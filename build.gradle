buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
        dockerVersion = '0.13.0'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:${dockerVersion}")
    }
}

group = 'app'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    archiveName '0.0.1.TEST.jar'
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'io.springfox:springfox-swagger2:2.7.0'
    compile 'io.springfox:springfox-swagger-ui:2.7.0'
    compile 'org.projectlombok:lombok'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile "com.github.tomakehurst:wiremock:2.6.0"
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

jacoco {
    toolVersion = '0.8.0'
}

jacocoTestReport {
    reports {
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'ifood/IfoodBackendAdvancedTest.class',
                'ifood/config/*.class',
                'ifood/utils/WiremockStarter.class'
            ])
        })
    }
}

docker {
    dependsOn build
    name "${project.group}/${jar.baseName}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

check.dependsOn jacocoTestReport